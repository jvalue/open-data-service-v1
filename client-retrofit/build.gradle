import org.jvalue.ods.api.auth.RestrictedTo
import de.bitdroid.jaxrs2retrofit.converter.ParamConverter
import de.bitdroid.jaxrs2retrofit.converter.AnnotatedParam
import de.bitdroid.jaxrs2retrofit.JaxRs2RetrofitTask
import com.squareup.javapoet.ClassName

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'de.bitdroid.jaxrs2retrofit:plugin:0.1.3'
        classpath 'org.jvalue.ods:models:0.1.0'
    }
}

dependencies {
    compile 'com.squareup.retrofit:retrofit:1.8.+'
    compile 'com.squareup.retrofit:converter-jackson:1.8.+'
    compile project(':models')

    testCompile 'org.jmockit:jmockit:1.13'
    testCompile 'junit:junit:4.11'
}


// JaxRs2Retrofit
task jaxrs2retrofit(type: JaxRs2RetrofitTask) {
    inputDir = file(project(':server').projectDir.toString() + '/' + mainSourcesDir)
    outputDir = file(generatedSourcesDir)
    retrofitPackageName = 'org.jvalue.ods.api'
    excludedClassNamesRegex = 'PluginApi|FilterDescriptionApi|Admin.*'
    paramConverterManager.registerConverter ClassName.get(RestrictedTo.class), new ParamConverter() {
        @Override
        AnnotatedParam convert(AnnotatedParam param) {
            return null;
        }
    };

}
project.compileJava.dependsOn jaxrs2retrofit

sourceSets {
    main {
        java {
            srcDir mainSourcesDir
            srcDir generatedSourcesDir
        }
    }
}

idea {
    module {
        sourceDirs += jaxrs2retrofit.outputDir
    }
}
